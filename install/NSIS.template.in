; Installation Script for DesignerVersionChooser

SetCompressor /SOLID lzma

!define PRODUCT_NAME "DesignerVersionChooser"
!define PRODUCT_VERSION "1.0.0.1"
!define PRODUCT_PUBLISHER "Pharos Labs"
!define PRODUCT_WEB_SITE "https://github.com/pharos-labs"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PRODUCT_NAME}.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

!define INSTDIR_REG_ROOT "HKLM"
!define INSTDIR_REG_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"

!define UNINST_EXE "Uninstall.exe"

!define FILE_PROJECT_CLASS "PharosControls\Designer.Project"
!define FILE_PROJECT_EXTENSION "pd2"

!include Integration.nsh
!include LogicLib.nsh
!include MUI.nsh
!include nsDialogs.nsh
!include WinCore.nsh

; MUI Settings
!define MUI_ABORTWARNING
@CPACK_NSIS_INSTALLER_MUI_ICON_CODE@
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_RUN $INSTDIR\${PRODUCT_NAME}.exe

Name "${PRODUCT_NAME}"
OutFile "@CPACK_TOPLEVEL_DIRECTORY@/@CPACK_OUTPUT_FILE_NAME@"

InstallDir "$PROGRAMFILES64\${PRODUCT_NAME}"
InstallDirRegKey ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "InstallDir"

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_DIRECTORY

# Custom page to deal with file association
Var PerformFileAssociation

Page custom pgPageCreate pgPageLeave

Var Dialog
Var Label1
Var Label2
Var CheckBox

Function pgPageCreate
    !insertmacro MUI_HEADER_TEXT "File Association" "Associate Designer files with the program"

    nsDialogs::Create 1018
    Pop $Dialog

    ${If} $Dialog == error
        Abort
    ${EndIf}

    ${NSD_CreateLabel} 0 10u 100% 12u "Associate Pharos Designer (.pd2) files with the application?"
	Pop $Label1

    ${NSD_CreateLabel} 0 25u 100% 24u "This will mean that when you double click a Designer project, the application will open to allow you to chose a version of designer"
	Pop $Label2

    ${NSD_CreateCheckBox} 0 50u 100% 15u "Associate PD2 Files"
	Pop $CheckBox

    nsDialogs::Show
FunctionEnd

Function PgPageLeave
    ${NSD_GetState} $CheckBox $PerformFileAssociation
FunctionEnd

!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"

Section "Main Application" sec01

SetOutPath '$INSTDIR'

File "..\..\..\Release\DesignerVersionChooser.exe"

;Shortcuts
CreateDirectory '$SMPROGRAMS\${PRODUCT_NAME}'
CreateShortcut '$SMPROGRAMS\${PRODUCT_NAME}.lnk' '$INSTDIR\${PRODUCT_NAME}.exe'

WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "InstallDir" "$INSTDIR"
WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "DisplayName" "${PRODUCT_NAME}"
WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "UninstallString" "$INSTDIR\${UNINST_EXE}"

WriteUninstaller "$INSTDIR\Uninstall.exe"

SectionEnd

Section -ShellAssoc
  ; Write the project file association
  ${If} $PerformFileAssociation == 1
    DetailPrint "Adding File Association Keys"
    WriteRegStr HKCR "${FILE_PROJECT_CLASS}\shell" "" "open"
    WriteRegStr HKCR "${FILE_PROJECT_CLASS}\shell\open\command" "" '"$INSTDIR\${PRODUCT_NAME}.exe" "%1"'
    ${NotifyShell_AssocChanged}
  ${Else}
    DetailPrint "Not Adding File Association Keys"
  ${EndIf}
SectionEnd



#######################################################################################

Section UnInstall

    Delete "$INSTDIR\DesignerVersionChooser.exe"
    Delete "$INSTDIR\${UNINST_EXE}"
    RmDir "$INSTDIR"
    Delete "$SMPROGRAMS\${PRODUCT_NAME}\${PRODUCT_NAME}.lnk"
    Delete "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk"
    RmDir "$SMPROGRAMS\${PRODUCT_NAME}"
    DeleteRegKey ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}"

SectionEnd
